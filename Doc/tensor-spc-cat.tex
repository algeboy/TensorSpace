
In Magma a tensor space is a parent type for tensors.  It behaves
as a module but also maintains an interpretation of its elements as
a multilinear map.  Each tensor space further maintains a tensor category
which is assigned to its tensors.


\section{Constructions of tensor and cotensor spaces}

\subsection{Universal tensor spaces}

Construction of universal tensor spaces is modeled after construction of free 
modules and matrix spaces. For efficiency reasons, the actual representation 
may 
vary based on the parameters, e.g. it may be a space of structure constants, 
black-box functions, or systems of forms. So access to the tensors in these 
tensor space should be made through the provided functions.

\color{blue}
\index{KTensorSpace}
{\small \begin{verbatim}
KTensorSpace(K, S) : Fld, [RngIntElt] -> TenSpc
KTensorSpace(K, S, C) : Fld, [RngIntElt], TenCat -> TenSpc
\end{verbatim} }
\color{black}

For a field $K$ and sequence $S=[d_v,\cdots, d_0]$, 
returns the universal tensor space $\hom_K(K^{d_v},\dots ,\hom_K(K^{d_1},K^{d_0})\cdots )\cong K^{d_v\cdots d_0}$
with covariant tensor category given by $C$.
The default is Albert's homotopism category.

\color{blue}
\index{RTensorSpace}
{\small \begin{verbatim}
RTensorSpace(R, S) : Rng, [RngIntElt] -> TenSpc
RTensorSpace(R, S, C) : Rng, [RngIntElt], TenCat -> TenSpc
\end{verbatim} }
\color{black}

For a commutative and unital ring $R$ and sequence $S=[d_v,\cdots, d_0]$, 
returns the universal tensor space $\hom_R(R^{d_v},\dots ,\hom_R(R^{d_1},R^{d_0})\cdots )\cong R^{d_v\cdots d_0}$
with covariant tensor category given by $C$.
The default is Albert's homotopism category.

\color{blue}
\index{TensorSpace}
{\small \begin{verbatim}
TensorSpace(S) : SeqEnum -> TenSpc
TensorSpace(S) : List -> TenSpc
TensorSpace(S, C) : SeqEnum, TenCat -> TenSpc
TensorSpace(S, C) : List, TenCat -> TenSpc
\end{verbatim} }
\color{black}

Given a sequence $S=[U_v,\dots, U_0]$ of $K$-modules returns a universal tensor 
space equivalent to $\hom_K(U_v\otimes_K\cdots\otimes_K U_1,U_0)$ with covariant tensor 
category given by $C$. The default is Albert's homotopism category. 

\color{blue}
\index{TensorSpace!signatured}
{\small \begin{verbatim}
TensorSpace(V, p, q) : ModTupFld, RngIntElt, RngIntElt -> TenSpc
TensorSpace(K, d, p, q) : Fld, RngIntElt, RngIntElt, RngIntElt -> TenSpc
\end{verbatim} }
\color{black}

Returns the signatured $(p,q)$-tensor space over the vector space $V=K^d$. 
The first $p$ indices are covariant and the last $q$ indices are contravariant.  
This is functionally equivalent
to creating a universal tensor space from the sequence 
$[V,\dots,_p V, V^*,\dots,_q V^*]$
and the tensor category with arrows $[1,\dots,_p 1, -1,\dots,_q -1]$ and 
duplicates $\{\{p+q,\dots,1+q\},\{q,\dots,1\},\{0\}\}$.

%%Example{Name}
\begin{framed}{\bf Example} {\tt TenSpc\_Const}\\
{\small We demonstrate how to construct universal tensor spaces.
\begin{lstlisting}[frame=single,basicstyle=\ttfamily\color{black!30!
teal},backgroundcolor=\color{white!70!gray}]
> TS := KTensorSpace(Rationals(),[ i : i in [3..7] ]);
> TS;
Tensor space of dimension 2520 over Rational Field with 
valence 4
U4 : Full Vector space of degree 3 over Rational Field
U3 : Full Vector space of degree 4 over Rational Field
U2 : Full Vector space of degree 5 over Rational Field
U1 : Full Vector space of degree 6 over Rational Field
U0 : Full Vector space of degree 7 over Rational Field
> 
> TS.1;
Tensor of valence 4, U4 x U3 x U2 x U1 >-> U0
U4 : Full Vector space of degree 3 over Rational Field
U3 : Full Vector space of degree 4 over Rational Field
U2 : Full Vector space of degree 5 over Rational Field
U1 : Full Vector space of degree 6 over Rational Field
U0 : Full Vector space of degree 7 over Rational Field
\end{lstlisting} 
Give a tensor space a frame.
\begin{lstlisting}[frame=single,basicstyle=\ttfamily\color{black!30!
teal},backgroundcolor=\color{white!70!gray}]
> R := pAdicRing(3,6);
> Fr := [RSpace(R,5), sub<RSpace(R,3)|[0,1,0],[0,0,1]>,\
>   RSpace(R,2)];
> TS := TensorSpace(Fr);
> TS;
Tensor space of dimension 20 over 3-adic ring mod 3^6 with 
valence 2
U2 : Full RSpace of degree 5 over pAdicRing(3, 6)
U1 : RSpace of degree 3, dimension 2 over pAdicRing(3, 6)
Generators:
(0 1 0)
(0 0 1)
Echelonized basis:
(0 1 0)
(0 0 1)
U0 : Full RSpace of degree 2 over pAdicRing(3, 6)
> 
> TS.10;
Tensor of valence 2, U2 x U1 >-> U0
U2 : Full RSpace of degree 5 over pAdicRing(3, 6)
U1 : RSpace of degree 3, dimension 2 over pAdicRing(3, 6)
Generators:
(0 1 0)
(0 0 1)
Echelonized basis:
(0 1 0)
(0 0 1)
U0 : Full RSpace of degree 2 over pAdicRing(3, 6)
\end{lstlisting}
With signatured tensor spaces, the tensor category is not immediately obvious at print out. 
Instead, one can glean categorical information using {\tt TensorCategory} on a tensor space.
\begin{lstlisting}[frame=single,basicstyle=\ttfamily\color{black!30!
teal},backgroundcolor=\color{white!70!gray}]
> TS := TensorSpace( VectorSpace(GF(3),3), 2, 4 );
> TS;
Tensor space of dimension 729 over GF(3) with valence 6
U6 : Full Vector space of degree 3 over GF(3)
U5 : Full Vector space of degree 3 over GF(3)
U4 : Full Vector space of degree 3 over GF(3)
U3 : Full Vector space of degree 3 over GF(3)
U2 : Full Vector space of degree 3 over GF(3)
U1 : Full Vector space of degree 3 over GF(3)
U0 : Full Vector space of degree 1 over GF(3)
> 
> TensorCategory(TS);
Tensor category of Valence 6 (<-,<-,->,->,->,->,==)
({ 0 },{ 5 .. 6 },{ 1 .. 4 })
\end{lstlisting}
We see that the indices 5 and 6 are contravariant and both are linked together as well. Furthermore the covariant indices are 1--4, which are also linked together.
}
\end{framed}

\subsection{Universal cotensor spaces}~

We only consider cotensor spaces over fields.

\color{blue}
\index{KCotensorSpace}
{\small \begin{verbatim}
KCotensorSpace(K, S) : Fld, [RngIntElt] -> TenSpc
KCotensorSpace(K, S, C) : Fld, [RngIntElt], TenCat -> TenSpc
\end{verbatim} }
\color{black}

For a field $K$ and sequence $S=[d_v,\dots, d_1]$ returns the universal cotensor 
space $\hom_K(K^{d_v}\otimes \cdots \otimes K^{d_1},K)\cong K^{d_v\cdots d_1}$ with the given contravariant tensor category $C$.
The default is Albert's homotopism category.

\color{blue}
\index{CotensorSpace}
{\small \begin{verbatim}
CotensorSpace(S) : SeqEnum -> TenSpc
CotensorSpace(S) : List -> TenSpc
CotensorSpace(S, C) : SeqEnum, TenCat -> TenSpc
CotensorSpace(S, C) : List, TenCat -> TenSpc
\end{verbatim} }
\color{black}

Given a sequence $S=[U_v,\dots, U_1]$ of $K$-vector spaces returns the universal tensor 
space equivalent to $\hom_K(U_v\otimes_K\cdots\otimes_K U_1,K)$ with contravariant tensor 
category given by $C$. The default is Albert's homotopism category.

%%Example{Name}
\begin{framed}{\bf Example} {\tt CoTenSpc\_Const}\\
{\small We construct cotensor spaces in the same way as we do tensor spaces.
\begin{lstlisting}[frame=single,basicstyle=\ttfamily\color{black!30!
teal},backgroundcolor=\color{white!70!gray}]
> CT := KCotensorSpace(GF(2),[ i : i in [5..7] ]);
> CT;
Cotensor space of dimension 210 over GF(2) with valence 2
U3 : Full Vector space of degree 5 over GF(2)
U2 : Full Vector space of degree 6 over GF(2)
U1 : Full Vector space of degree 7 over GF(2)
> 
> CT.1;
Cotensor of valence 2, U3 x U2 x U1 >-> K
U3 : Full Vector space of degree 5 over GF(2)
U2 : Full Vector space of degree 6 over GF(2)
U1 : Full Vector space of degree 7 over GF(2)
> 
> 
> Cat := CotensorCategory([1,0,-1],{{1},{2},{3}});
> Fr := [ VectorSpace(GF(8),4) : i in [1..3] ];
> CT := CotensorSpace( Fr, Cat );
> CT;
Cotensor space of dimension 64 over GF(2^3) with valence 2
U3 : Full Vector space of degree 4 over GF(2^3)
U2 : Full Vector space of degree 4 over GF(2^3)
U1 : Full Vector space of degree 4 over GF(2^3)
> 
> TensorCategory(CT);
Cotensor category of valence 2 (->,==,<-) ({ 1 },{ 2 },{ 3 })
\end{lstlisting}
}
\end{framed}

\subsection{Some standard constructions}

We include some subspaces generated by well-known tensors.

\color{blue}
\index{AlternatingSpace}
{\small \begin{verbatim}
AlternatingSpace(T) : TenSpc -> TenSpc
\end{verbatim} }
\color{black}

Returns the sub(co-)tensor space generated by all the alternating (co-)tensors contained in the given (co-)tensor space.

\color{blue}
\index{AntisymmetricSpace}
{\small \begin{verbatim}
AntisymmetricSpace(T) : TenSpc -> TenSpc
\end{verbatim} }
\color{black}

Returns the sub(co-)tensor space generated by all the antisymmetric (co-)tensors contained in the given (co-)tensor space.

\color{blue}
\index{SymmetricSpace}
{\small \begin{verbatim}
SymmetricSpace(T) : TenSpc -> TenSpc
\end{verbatim} }
\color{black}

Returns the sub(co-)tensor space generated by all the symmetric (co-)tensors contained in the given (co-)tensor space.

\color{blue}
\index{ExteriorCotensorSpace}
{\small \begin{verbatim}
ExteriorCotensorSpace(V, n) : ModTupFld, RngIntElt -> TenSpc
\end{verbatim} }
\color{black}

Returns the cotensor space given by the $n$th exterior power of the vector space $V$.

\color{blue}
\index{SymmetricCotensorSpace}
{\small \begin{verbatim}
SymmetricCotensorSpace(V, n) : ModTupFld, RngIntElt -> TenSpc
\end{verbatim} }
\color{black}

Returns the cotensor space given by the $n$th symmetric power of the vector space $V$.

%%Example{Name}
\begin{framed}{\bf Example} {\tt TenSpc\_Const2}\\
{\small We construct the symmetric subtensor space of the universal tensor space.
\begin{lstlisting}[frame=single,basicstyle=\ttfamily\color{black!30!
teal},backgroundcolor=\color{white!70!gray}]
> TS := KTensorSpace(GF(3), [3,3,3,2]);
> TS;
Tensor space of dimension 54 over GF(3) with valence 3
U3 : Full Vector space of degree 3 over GF(3)
U2 : Full Vector space of degree 3 over GF(3)
U1 : Full Vector space of degree 3 over GF(3)
U0 : Full Vector space of degree 2 over GF(3)
> 
> SS := SymmetricSpace(TS);
> AsMatrices(Random(SS),3,2);
[
    [1 1 1]
    [1 0 0]
    [1 0 0],

    [2 2 2]
    [2 0 1]
    [2 1 0],

    [1 0 0]
    [0 1 2]
    [0 2 2],

    [2 0 1]
    [0 1 1]
    [1 1 2],

    [1 0 0]
    [0 2 2]
    [0 2 1],

    [2 1 0]
    [1 1 2]
    [0 2 0]
]
\end{lstlisting}
For $V=\mathbb{F}_{25}^6$, we construct the fourth exterior power of $V$, $\Lambda^4(V)$ 
as a sub cotensor space of dimension $\binom{6}{4}=15$.
\begin{lstlisting}[frame=single,basicstyle=\ttfamily\color{black!30!
teal},backgroundcolor=\color{white!70!gray}]
> V := VectorSpace(GF(25),6);
> E := ExteriorCotensorSpace(V,4);
> E;
Cotensor space of dimension 15 over GF(5^2) with valence 3
U4 : Full Vector space of degree 6 over GF(5^2)
U3 : Full Vector space of degree 6 over GF(5^2)
U2 : Full Vector space of degree 6 over GF(5^2)
U1 : Full Vector space of degree 6 over GF(5^2)
> 
> T := Random(E);
> IsAlternating(T);
true
\end{lstlisting} }
\end{framed}

\subsection{Subspaces as closures}~

\color{blue}
\index{DerivationClosure}
{\small \begin{verbatim}
DerivationClosure(TS, O) : TenSpc, AlgMat -> TenSpc
DerivationClosure(TS, O) : TenSpc, ModMatFld -> TenSpc
DerivationClosure(TS, O) : TenSpc, AlgMatLie -> TenSpc
DerivationClosure(TS, O) : TenSpc, [Mtrx] -> TenSpc
DerivationClosure(TS, O) : TenSpc, [AlgMatLie] -> TenSpc
\end{verbatim} }
\color{black}

Returns the derivation closure of the given tensor space $TS$, with frame $U_2\times U_1\rightarrowtail U_0$, with operators $O\subseteq \text{End}(U_2)\times \text{End}(U_1)\times \text{End}(U_0)$.
Currently, this only works for tensor spaces of valence 2.
This is the subspace whose tensors' derivation algebra contains $O$.

\color{blue}
\index{DerivationClosure}
{\small \begin{verbatim}
DerivationClosure(TS, T) : TenSpc, TenSpcElt -> TenSpc
\end{verbatim} }
\color{black}

Returns the derivation closure of the given tensor space $TS$, with frame $U_2\times U_1\rightarrowtail U_0$, with operators $O\subseteq \text{End}(U_2)\times \text{End}(U_1)\times \text{End}(U_0)$.
Currently, this only works for tensor spaces of valence 2.
This is the subspace whose tensors' derivation algebra contains the derivation algebra of $T$.

\color{blue}
\index{NucleusClosure}
{\small \begin{verbatim}
NucleusClosure(TS, O, s, t) : TenSpc, AlgMat, RngIntElt, RngIntElt -> TenSpc
NucleusClosure(TS, O, s, t) : TenSpc, ModMatFld, RngIntElt, RngIntElt -> TenSpc
NucleusClosure(TS, O, s, t) : TenSpc, [Mtrx], RngIntElt, RngIntElt -> TenSpc
\end{verbatim} }
\color{black}

Returns the nucleus closure of the tensor space $TS$, with frame $U_2\times U_1\rightarrowtail U_0$, with operators $O\subseteq \text{End}(U_s)\times \text{End}(U_t)$.
Currently, this only works for tensor spaces of valence 2.
This returns the subspace whose tensors' $st$-nuclues contains $O$.

\color{blue}
\index{NucleusClosure}
{\small \begin{verbatim}
NucleusClosure(TS, T, s, t) : TenSpc, TenSpcElt, RngIntElt, RngIntElt -> TenSpc
\end{verbatim} }
\color{black}

Returns the nucleus closure of the tensor space $TS$, with frame $U_2\times U_1\rightarrowtail U_0$, with operators $O\subseteq \text{End}(U_s)\times \text{End}(U_t)$.
Currently, this only works for tensor spaces of valence 2.
This returns the subspace whose tensors' $st$-nuclues contains the $st$-nucleus of $T$.

%%Example{Name}
\begin{framed}{\bf Example} {\tt Der\_Closure}\\
{\small We illustrate the fact that the (hyper-)matrix multiplication is unique. 
The derivation closure of the tensor in the universal tensor space is 1-dimensional.
\begin{lstlisting}[frame=single,basicstyle=\ttfamily\color{black!30!
teal},backgroundcolor=\color{white!70!gray}]
> Fr := [ KMatrixSpace(GF(3),2,3),
>   KMatrixSpace(GF(3),3,2),KMatrixSpace(GF(3),2,2) ];
> F := func< x | x[1]*x[2] >;
> T := Tensor(Fr,F);
> T;
Tensor of valence 2, U2 x U1 >-> U0
U2 : Full KMatrixSpace of 2 by 3 matrices over GF(3)
U1 : Full KMatrixSpace of 3 by 2 matrices over GF(3)
U0 : Full KMatrixSpace of 2 by 2 matrices over GF(3)
> 
> TS := Parent(T);
> TS;
Tensor space of dimension 144 over GF(3) with valence 2
U2 : Full Vector space of degree 6 over GF(3)
U1 : Full Vector space of degree 6 over GF(3)
U0 : Full Vector space of degree 4 over GF(3)
> 
> D := DerivationClosure(TS,T);
> D;
Tensor space of dimension 1 over GF(3) with valence 2
U2 : Full Vector space of degree 6 over GF(3)
U1 : Full Vector space of degree 6 over GF(3)
U0 : Full Vector space of degree 4 over GF(3)
\end{lstlisting} }
\end{framed}

%%Example{Name}
\begin{framed}{\bf Example} {\tt Nuc\_Closure}\\
{\small We illustrate that the commutator from the Heisenberg group is unique over the correct field.
\begin{lstlisting}[frame=single,basicstyle=\ttfamily\color{black!30!
teal},backgroundcolor=\color{white!70!gray}]
> H := ClassicalSylow( GL(3,125), 5 ); // Heisenberg group
> T := pCentralTensor(H,5,1,1);
> T;
Tensor of valence 2, U2 x U1 >-> U0
U2 : Full Vector space of degree 6 over GF(5)
U1 : Full Vector space of degree 6 over GF(5)
U0 : Full Vector space of degree 3 over GF(5)
\end{lstlisting}
The centroid of $T$ will be 3-dimensional and is isomorphic to $\mathbb{F}_{125}$.
\begin{lstlisting}[frame=single,basicstyle=\ttfamily\color{black!30!
teal},backgroundcolor=\color{white!70!gray}]
> C := Centroid(T);
> C;
Matrix Algebra of degree 15 with 3 generators over GF(5)
> 
> S := TensorOverCentroid(T);
> S;
Tensor of valence 2, U2 x U1 >-> U0
U2 : Full Vector space of degree 2 over GF(5^3)
U1 : Full Vector space of degree 2 over GF(5^3)
U0 : Full Vector space of degree 1 over GF(5^3)
> 
> TS := Parent(S);
> N := NucleusClosure(TS,S,2,1);
> N;
Tensor space of dimension 1 over GF(5^3) with valence 2
U2 : Full Vector space of degree 2 over GF(5^3)
U1 : Full Vector space of degree 2 over GF(5^3)
U0 : Full Vector space of degree 1 over GF(5^3)
\end{lstlisting} 
Compare this closure with the closure of our original tensor over $\mathbb{F}_5$.
\begin{lstlisting}[frame=single,basicstyle=\ttfamily\color{black!30!
teal},backgroundcolor=\color{white!70!gray}]
> NT := NucleusClosure(Parent(T),T,2,1);
> NT;
Tensor space of dimension 36 over GF(5) with valence 2
U2 : Full Vector space of degree 6 over GF(5)
U1 : Full Vector space of degree 6 over GF(5)
U0 : Full Vector space of degree 3 over GF(5)
\end{lstlisting} }
\end{framed}


\section{Operations on tensor spaces}

\subsection{Membership and comparison with tensor spaces}

We define some intuitive functions for tensor spaces, similar to those found for modules.

\color{blue}
\index{in}
{\small \begin{verbatim}
T in TS : TenSpcElt, TenSpc -> BoolElt
\end{verbatim} }
\color{black}

Decides if $T$ is contained in the tensor space $TS$.

\color{blue}
\index{IsCoercible!tensor space}
{\small \begin{verbatim}
IsCoercible(TS, T) : TenSpc, TenSpcElt -> BoolElt, TenSpcElt
TS ! T : TenSpc, TenSpcElt -> BoolElt, TenSpcElt
\end{verbatim} }
\color{black}

Decides if the tensor $T$ can be coerced into the tensor space $TS$. If so, the tensor is returned as an element of $TS$.

\color{blue}
\index{IsCoercible!tensor space}
{\small \begin{verbatim}
IsCoercible(TS, S) : TenSpc, SeqEnum -> BoolElt, TenSpcElt
TS ! S : TenSpc, SeqEnum -> BoolElt, TenSpcElt
\end{verbatim} }
\color{black}

Decides if the sequence $S$ can be coerced into the tensor space $TS$ as a tensor. If so, the corresponding tensor is returned. 

\color{blue}
\index{eq!tensor space}
{\small \begin{verbatim}
S eq T : TenSpc, TenSpc -> BoolElt
\end{verbatim} }
\color{black}

Decides if the tensor spaces $S$ and $T$ are equal.

\color{blue}
\index{subset}
{\small \begin{verbatim}
S subset T : TenSpc, TenSpc -> BoolElt
\end{verbatim} }
\color{black}

Decides if $S$ is a subset of the tensor space $T$.

\begin{framed}{\bf Example} {\tt TenCoerce}\\
{\small We illustrate that using {\tt !} is the same as creating the tensor from scratch.
\begin{lstlisting}[frame=single,basicstyle=\ttfamily\color{black!30!
teal},backgroundcolor=\color{white!70!gray}]
> TS := KTensorSpace( GF(2), [2,3,2] );
> TS;
Tensor space of dimension 12 over GF(2) with valence 2
U2 : Full Vector space of degree 2 over GF(2)
U1 : Full Vector space of degree 3 over GF(2)
U0 : Full Vector space of degree 2 over GF(2)
> 
> S := [ Random(GF(2)) : i in [1..12] ];
> T := TS!S;
> T;
Tensor of valence 2, U2 x U1 >-> U0
U2 : Full Vector space of degree 2 over GF(2)
U1 : Full Vector space of degree 3 over GF(2)
U0 : Full Vector space of degree 2 over GF(2)
> 
> T eq Tensor(GF(2),[2,3,2],S);
true
\end{lstlisting}
We demonstrate how to coerce into the symmetric cube of $V=\mathbb{Q}^{10}$ and construct a subcotensor space.
\begin{lstlisting}[frame=single,basicstyle=\ttfamily\color{black!30!
teal},backgroundcolor=\color{white!70!gray}]
> V := VectorSpace(Rationals(),10);
> SS := SymmetricCotensorSpace(V,3);
> SS;
Cotensor space of dimension 220 over Rational Field with valence 2
U3 : Full Vector space of degree 10 over Rational Field
U2 : Full Vector space of degree 10 over Rational Field
U1 : Full Vector space of degree 10 over Rational Field
> 
> CT := SubtensorSpace(SS,SS![1..1000]);
> CT;
Cotensor space of dimension 1 over Rational Field with valence 2
U3 : Full Vector space of degree 10 over Rational Field
U2 : Full Vector space of degree 10 over Rational Field
U1 : Full Vector space of degree 10 over Rational Field
> 
> CT subset SS;
true
> CT.1 in SS;
true
> SS.2 in CT;
false
\end{lstlisting}
}
\end{framed}

\subsection{Tensor spaces as modules}

We view a tensor space as a $K$-module, so we have notions of generators, 
dimension (if it is free), and cardinality.

\color{blue}
\index{Generators}
{\small \begin{verbatim}
Generators(T) : TenSpc -> SeqEnum
\end{verbatim} }
\color{black}

Returns a sequence of generators for the tensor space. 

\color{blue}
\index{.}
{\small \begin{verbatim}
T . i : TenSpc, RngIntElt -> TenSpcElt
\end{verbatim} }
\color{black}

Returns the $i$th generator of the tensor space $T$.

\color{blue}
\index{NumberOfGenerators}\index{Ngens}
{\small \begin{verbatim}
NumberOfGenerators(T) : TenSpc -> RngIntElt
Ngens(T) : TenSpc -> RngIntElt
\end{verbatim} }
\color{black}

Returns the number of generators of the tensor space $T$.

\color{blue}
\index{Dimension}
{\small \begin{verbatim}
Dimension(T) : TenSpc -> RngIntElt
\end{verbatim} }
\color{black}

Returns the dimension of the tensor space $T$ as a free $K$-module.

\color{blue}
\index{\#}
{\small \begin{verbatim}
# T : TenSpc -> RngIntElt
\end{verbatim} }
\color{black}

Returns the size of the tensor space, provided it is finite.

\color{blue}
\index{Random}
{\small \begin{verbatim}
Random(T) : TenSpc -> TenSpcElt
\end{verbatim} }
\color{black}

Provided the base ring has a random algorithm in Magma, it returns a random 
element of the tensor space $T$.

\color{blue}
\index{RandomTensor}
{\small \begin{verbatim}
RandomTensor(R, S) : Rng, [RngIntElt] -> TenSpcElt
RandomTensor(R, S, C) : Rng, [RngIntElt], TenCat -> TenSpcElt
\end{verbatim} }
\color{black}

Provided $R$ has a random algorithm in Magma, it returns a random tensor from 
the tensor space $\hom_R(R^{d_v},\dots,\hom_R(R^{d_1},R^{d_0})\cdots )$ with category $C$.
The default category is the homotopism category.

\begin{framed}{\bf Example} {\tt TenSpc\_Module}\\
{\small We demonstrate the module functions for a tensor space.
\begin{lstlisting}[frame=single,basicstyle=\ttfamily\color{black!30!
teal},backgroundcolor=\color{white!70!gray}]
> TS := KTensorSpace( GF(9), [2,2,2,2] );
> TS;
Tensor space of dimension 16 over GF(3^2) with valence 3
U3 : Full Vector space of degree 2 over GF(3^2)
U2 : Full Vector space of degree 2 over GF(3^2)
U1 : Full Vector space of degree 2 over GF(3^2)
U0 : Full Vector space of degree 2 over GF(3^2)
> 
> Ngens(TS);
16
> #TS eq 9^Ngens(TS);
true
> 
> Eltseq(TS.2);
[ 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0 ]
\end{lstlisting}
We can use {\tt RandomTensor} to get a random tensor or cotensor from scratch.
\begin{lstlisting}[frame=single,basicstyle=\ttfamily\color{black!30!
teal},backgroundcolor=\color{white!70!gray}]
> T := RandomTensor(GF(3),[2,2,2]);
> T;
Tensor of valence 2, U2 x U1 >-> U0
U2 : Full Vector space of degree 2 over GF(3)
U1 : Full Vector space of degree 2 over GF(3)
U0 : Full Vector space of degree 2 over GF(3)
> 
> Cat := CotensorCategory([1,1,1],{{1,2,3}});
> T := RandomTensor(GF(3),[2,2,2],Cat);
> T;
Cotensor of valence 2, U3 x U2 x U1 >-> K
U3 : Full Vector space of degree 2 over GF(3)
U2 : Full Vector space of degree 2 over GF(3)
U1 : Full Vector space of degree 2 over GF(3)
\end{lstlisting}
}
\end{framed}

\subsection{Properties of tensor spaces}

We define some functions to access basic properties of tensor spaces.

\color{blue}
\index{Valence!tensor space}
{\small \begin{verbatim}
Valence(T) : TenSpc -> RngIntElt
\end{verbatim} }
\color{black}

Returns the valence of the tensor space.

\color{blue}
\index{Frame!tensor space}
{\small \begin{verbatim}
Frame(T) : TenSpc -> List
\end{verbatim} }
\color{black}

Returns the list of modules in the frame of the tensor space.

\color{blue}
\index{BaseRing!tensor space}\index{BaseField!tensor space}
{\small \begin{verbatim}
BaseRing(T) : TenSpc -> Rng
BaseField(T) : TenSpc -> Fld
\end{verbatim} }
\color{black}

Returns the base ring (or field) of the tensor space.

\color{blue}
\index{TensorCategory!tensor space}
{\small \begin{verbatim}
TensorCategory(T) : TenSpc -> TenCat
\end{verbatim} }
\color{black}

Returns the underlying tensor category of the tensor space.

\color{blue}
\index{IsCovariant!tensor space}\index{IsContravariant!tensor space}
{\small \begin{verbatim}
IsCovariant(T) : TenSpc -> BoolElt
IsContravariant(T) : TenSpc -> BoolElt
\end{verbatim} }
\color{black}

Decides if the underlying tensor category is covariant or contravariant.

\color{blue}
\index{ChangeTensorCategory!tensor space}
{\small \begin{verbatim}
ChangeTensorCategory(T, C) : TenSpc, TenCat -> TenSpc
ChangeTensorCategory(~T, C) : TenSpc, TenCat
\end{verbatim} }
\color{black}

Returns the tensor category with the given tensor category.

\color{blue}
\index{IsAlternating!tensor space}
{\small \begin{verbatim}
IsAlternating(T) : TenSpc -> BoolElt
\end{verbatim} }
\color{black}

Decides if every tensor in the tensor space is an alternating tensor.

\color{blue}
\index{IsAntisymmetric!tensor space}
{\small \begin{verbatim}
IsAntisymmetric(T) : TenSpc -> BoolElt
\end{verbatim} }
\color{black}

Decides if every tensor in the tensor space is an antisymmetric tensor.

\color{blue}
\index{IsSymmetric!tensor space}
{\small \begin{verbatim}
IsSymmetric(T) : TenSpc -> BoolElt
\end{verbatim} }
\color{black}

Decides if every tensor in the tensor space is a symmetric tensor.

\color{blue}
\index{UniversalTensorSpace}\index{UniversalCotensorSpace}\index{Generic}
{\small \begin{verbatim}
UniversalTensorSpace(T) : TenSpc -> TenSpc
UniversalCotensorSpace(T) : TenSpc -> TenSpc
Generic(T) : TenSpc -> TenSpc
\end{verbatim} }
\color{black}

Returns the universal (co-)tensor space with the same frame and category as $T$.

\begin{framed}{\bf Example} {\tt TenSpc\_Prop}\\
{\small We demonstrate the functions to obtain standard tensor space properties.
\begin{lstlisting}[frame=single,basicstyle=\ttfamily\color{black!30!
teal},backgroundcolor=\color{white!70!gray}]
> TS := KTensorSpace( GF(23), [3,4,5,6] );
> TS;
Tensor space of dimension 360 over GF(23) with valence 3
U3 : Full Vector space of degree 3 over GF(23)
U2 : Full Vector space of degree 4 over GF(23)
U1 : Full Vector space of degree 5 over GF(23)
U0 : Full Vector space of degree 6 over GF(23)
> 
> Valence(TS);
3
> Frame(TS);
[*
    Full Vector space of degree 3 over GF(23),

    Full Vector space of degree 4 over GF(23),

    Full Vector space of degree 5 over GF(23),

    Full Vector space of degree 6 over GF(23)
*]
> TensorCategory(TS);
Tensor category of Valence 3 (->,->,->,->) 
({ 1 },{ 2 },{ 0 },{ 3 })
> 
> Cat := TensorCategory([1,1,-1,-1],{{0},{1},{2},{3}});
> ChangeTensorCategory(~TS,Cat);
> TensorCategory(TS);
Tensor category of Valence 3 (->,->,<-,<-) 
({ 1 },{ 2 },{ 0 },{ 3 })
\end{lstlisting}
We construct the universal cotensor space of the symmetric cube, $S^3(V)$, of a vector space $V$.
\begin{lstlisting}[frame=single,basicstyle=\ttfamily\color{black!30!
teal},backgroundcolor=\color{white!70!gray}]
> V := VectorSpace( GF(3), 5 );
> S := SymmetricCotensorSpace(V,3);
> S;
Cotensor space of dimension 30 over GF(3) with valence 2
U3 : Full Vector space of degree 5 over GF(3)
U2 : Full Vector space of degree 5 over GF(3)
U1 : Full Vector space of degree 5 over GF(3)
> UniversalCotensorSpace(S);
Cotensor space of dimension 125 over GF(3) with valence 2
U3 : Full Vector space of degree 5 over GF(3)
U2 : Full Vector space of degree 5 over GF(3)
U1 : Full Vector space of degree 5 over GF(3)
> 
> IsSymmetric(S);
true
\end{lstlisting}
}
\end{framed}

\chapter{Tensor categories}

Magma allows tensors and tensor spaces to change categories. 
Unless a user specifies otherwise,
all tensors are assigned a category that is natural to the method
by which it was created.
For example a tensor created from an algebra will be assigned an 
algebra category, whereas a tensor created by structure constants
will be assigned the Albert homotopism category.  Tensor categories influence
the behavior of commands such as kernels and images  
as well as the algebraic invariants such as derivation algebras of a tensor.  

Our conventions follow \cite{Wilson:division}.  In particular given
a tensor $T$ framed by $[U_{v},\dots,U_0]$ then a tensor category for
$T$ will specify a function $A:\{0\dots v\}\to \{-1,0,1\}$ along with
a partition $\mathcal{P}$ of $\{0,\dots,v\}$ such that the following 
rules apply to the tensors and morphisms in the category.
\begin{enumerate}
\item for each tensor $T$ framed by $[U_v,\dots,U_0]$, if
$X\in\mathcal{P}$, then 
$$\forall i,j\in X,\quad U_i=U_j.$$

\item Given a second tensor $S$ framed by $[V_v,\dots,V_0]$,
a morphism $f:T\to S$ (Magma type {\tt Hmtp}) will be a list
$[f_v,\dots,f_0]$ of homomorphisms as follows:
\begin{itemize}
\item (Covariant) if $A(i)=1$ then $f_i:U_i\to V_i$;
\item (Constant) if $A(i)=0$ then $U_i=V_i$ and $f_i=1_{U_i}$; or else
\item (Contravariant) $A(i)=-1$ and $f_i:U_i\leftarrow V_i$.
\end{itemize}
So if $A(0)=1$ then
\begin{align*}
	\left\langle \sum_{i\in A^{-1}(1)} u_i f_i
		+\sum_{j\not\in A^{-1}(-1)} v_j\right\rangle_S
		& = \left\langle  \sum_{i\in A^{-1}(1)} u_i 
		+\sum_{j\not\in A^{-1}(-1)} v_j f_j\right\rangle_T f_0;
\end{align*}
if $A(0)=0$ then
\begin{align*}
	\left\langle \sum_{i\in A^{-1}(1)} u_i f_i
		+\sum_{j\not\in A^{-1}(-1)} v_j\right\rangle_S
		& = \left\langle  \sum_{i\in A^{-1}(1)} u_i 
		+\sum_{j\not\in A^{-1}(-1)} v_j f_j\right\rangle_T;
\end{align*}
else $A(0)=-1$ and 
\begin{align*}
	\left\langle \sum_{i\in A^{-1}(1)} u_i f_i
		+\sum_{j\not\in A^{-1}(-1)} v_j\right\rangle_S f_0
		& = \left\langle  \sum_{i\in A^{-1}(1)} u_i 
		+\sum_{j\not\in A^{-1}(-1)} v_j f_j\right\rangle_T.
\end{align*}
\end{enumerate}

Magma manages internally the differences between 
vectors and covectors and more generally tensors and cotensors.
Both types are issued the Magma type {\tt TenSpcElt}.
For operations sensitive to the difference, Magma stores a value of
co/contra-variance of the tensor as a property of the tensor category.
This the third general property stored in Magma's tensor category type
{\tt TenCat}.

We use the phrase tensor category exclusively for categories that describe tensors and tensor spaces.
In other words, the data structure of a tensor category is a function 
$A:\{0,\dots,v\}\rightarrow \{-1,0,1\}$ and a partition $P$ of $\{0,\dots,v\}$.
It is useful to distinguish from tensors and cotensors at the categorical level,
so a tensor category is either covariant or contravariant as well 
(in the latter case, referred to as a cotensor category).

\section{Creating tensor categories}
\color{blue}
\index{TensorCategory!constructor}
{\small \begin{verbatim}
TensorCategory(A, P) : [RngIntElt], {SetEnum} -> TenCat
TensorCategory(A, P) : Map, {SetEnum} -> TenCat
\end{verbatim} }
\color{black}

Sets up a covariant tensor space category with specified direction of
arrows $A$, and a partition $P$ indicating variables to be treated as
equivalent.  The fiber $A^{-1}(1)$ denotes the covariant variables,
$A^{-1}(0)$ identifies the constant variables, and $A^{-1}(-1)$ marks
the contra-variant variables.  

\color{blue}
\index{CotensorCategory!constructor}
{\small \begin{verbatim}
CotensorCategory(A, P) : [RngIntElt], {SetEnum} -> TenCat
CotensorCategory(A, P) : Map, {SetEnum} -> TenCat
\end{verbatim} }
\color{black}

Sets up a contra-variant tensor space category with specified direction of
arrows $A$, and a partition $P$ indicating variables to be treated as
equivalent.  The fiber $A^{-1}(1)$ denotes the covariant variables,
$A^{-1}(0)$ identifies the constant variables, and $A^{-1}(-1)$ marks
the contra-variant variables.  

\color{blue}
\index{HomotopismCategory}
{\small \begin{verbatim}
HomotopismCategory(v : \parameters) : RngIntElt -> TenCat
Contravariant : BoolElt : false
\end{verbatim} }
\color{black}

Returns Albert's homotopism category -- all modules categories are covariant 
and 
no duplicates considered.
Set the optional parameter {\tt Contravariant} to {\tt true} to make it a cotensor category.

\color{blue}
\index{CohomotopismCategory}
{\small \begin{verbatim}
CohomotopismCategory(v) : RngIntElt -> TenCat
\end{verbatim} }
\color{black}

Returns the cohomotopism category -- all domain modules categories are 
covariant, the codomain is contravariant, and no duplicates considered.

\color{blue}
\index{AdjointCategory}\index{LinearCategory}
{\small \begin{verbatim}
AdjointCategory(v, s, t) :  RngIntElt, RngIntElt, RngIntElt -> TenCat
LinearCategory(v, s, t) :  RngIntElt, RngIntElt, RngIntElt -> TenCat
\end{verbatim} }
\color{black}

Returns the tensor category where all modules are constant except in position 
$s$ and $t$.  Both $s$ and $t$ are in $\{0,\dots, v\}$.
Position $s$ is covariant, position $t$ is contravariant.

\begin{framed}{\bf Example} {\tt TenCat\_Const}\\
{\small We illustrate how to construct tensor categories.
\begin{lstlisting}[frame=single,basicstyle=\ttfamily\color{black!30!
teal},backgroundcolor=\color{white!70!gray}]
> Cat := TensorCategory([1,-1,0],{{0},{1},{2}});
> Cat;
Tensor category of Valence 2 (->,<-,==) ({ 1 },{ 2 },{ 0 })
> 
> TS := KTensorSpace(GF(5),[5,3,4],Cat);
> TS;
Tensor space of dimension 60 over GF(5) with valence 2
U2 : Full Vector space of degree 5 over GF(5)
U1 : Full Vector space of degree 3 over GF(5)
U0 : Full Vector space of degree 4 over GF(5)
> TensorCategory(TS);
Tensor category of Valence 2 (->,<-,==) ({ 1 },{ 2 },{ 0 })
> 
> IsContravariant(TS);
false
\end{lstlisting}
All the tensor constructors that allow a {\tt TenCat} input can be used to make cotensors.
\begin{lstlisting}[frame=single,basicstyle=\ttfamily\color{black!30!
teal},backgroundcolor=\color{white!70!gray}]
> Cat := HomotopismCategory(2 : Contravariant := true);
> Cat;
Cotensor category of valence 2 (->,->) ({ 1 },{ 2 })
> T := Tensor(GF(5),[2,2],[1..4],Cat);
> T;
Cotensor of valence 1, U2 x U1 >-> K
U2 : Full Vector space of degree 2 over GF(5)
U1 : Full Vector space of degree 2 over GF(5)
\end{lstlisting}
}
\end{framed}

\section{Operations on tensor categories}

We have basic operations for tensor categories.

\color{blue}
\index{eq!tensor category}
{\small \begin{verbatim}
C1 eq C2 : TenCat, TenCat -> BoolElt
\end{verbatim} }
\color{black}

Decides if the tensor categories are the same.

\color{blue}
\index{Valence!tensor category}
{\small \begin{verbatim}
Valence(C) : TenCat -> RngIntElt
\end{verbatim} }
\color{black}

Returns the valence of the tensor category.

\color{blue}
\index{Arrows}
{\small \begin{verbatim}
Arrows(C) : TenCat -> SeqEnum
\end{verbatim} }
\color{black}

Returns the sequence of arrows of the tensor category. 
A $-1$ signifies an a contravariant index, a $0$ signifies a constant index, and a $1$ signifies a covariant index.

\color{blue}
\index{RepeatPartition}
{\small \begin{verbatim}
RepeatPartition(C) : TenCat -> SetEnum
\end{verbatim} }
\color{black}

Returns the repeat partition for the tensor category.

\color{blue}
\index{IsCovariant!tensor category}\index{IsContravariant!tensor category}
{\small \begin{verbatim}
IsCovariant(C) : TenCat -> BoolElt
IsContravariant(C) : TenCat -> BoolElt
\end{verbatim} }
\color{black}

Decides if the tensor category is covariant or contravariant.

\begin{framed}{\bf Example} {\tt TenCat\_Prop}\\
{\small We obtain basic properties of tensor categories.
\begin{lstlisting}[frame=single,basicstyle=\ttfamily\color{black!30!
teal},backgroundcolor=\color{white!70!gray}]
> C1 := TensorCategory([1,1,-1,1],{{0,3},{1},{2}});
> C1;
Tensor category of Valence 3 (->,->,<-,->) ({ 1 },{ 2 },
{ 0, 3 })
> 
> A := map< {0..3} -> {-1,0,1} | x :-> 1 >;
> C2 := TensorCategory(A,{{0..3}});
> C2;
Tensor category of Valence 3 (->,->,->,->) ({ 0 .. 3 })
> 
> C1 eq C2;
false
> RepeatPartition(C2);
{
    { 0 .. 3 }
}
> Valence(C2);
3
> Arrows(C2);
[ 1, 1, 1, 1 ]
\end{lstlisting}
}
\end{framed}

\section{Categorical operations}

\subsection{Categorical operations on tensors}

We include functions defined for the category of tensors.
Most functions are currently defined only for Albert's homotopism category.

\color{blue}
\index{Subtensor}
{\small \begin{verbatim}
Subtensor(T, S) : TenSpcElt, List -> TenSpcElt
\end{verbatim} }
\color{black}

Returns the smallest submap of $T$ containing $S$.

\color{blue}
\index{Subtensor}
{\small \begin{verbatim}
Subtensor(T, D, C) : TenSpcElt, List, Any -> TenSpcElt
\end{verbatim} }
\color{black}

Returns the smallest submap of $T$ containing $D$ in the domain and $C$ in the 
codomain.

\color{blue}
\index{IsSubtensor}
{\small \begin{verbatim}
IsSubtensor(T, S) : TenSpcElt, TenSpcElt -> BoolElt
\end{verbatim} }
\color{black}

Decides whether $S$ is a subtensor of $T$.

\color{blue}
\index{LocalIdeal}
{\small \begin{verbatim}
LocalIdeal(T, S, I) : TenSpcElt, List, {RngIntElt} -> TenSpcElt
\end{verbatim} }
\color{black}

Returns the local ideal of $T$ at $I$ constaining $S$.

\color{blue}
\index{LocalIdeal}
{\small \begin{verbatim}
LocalIdeal(T, D, C, I) : TenSpcElt, List, Any, {RngIntElt} -> TenSpcElt
\end{verbatim} }
\color{black}

Returns the local ideal of $T$ at $I$ constaining $D$ in the domain and $C$ 
in the codomain.

\color{blue}
\index{LocalIdeal}
{\small \begin{verbatim}
LocalIdeal(T, S, I) : TenSpcElt, TenSpcElt, {RngIntElt} -> TenSpcElt
\end{verbatim} }
\color{black}

Returns the local ideal of $T$ at $I$ constaining $S$ as a submap.


\color{blue}
\index{IsLocalIdeal}
{\small \begin{verbatim}
IsLocalIdeal(T, S, I) : TenSpcElt, TenSpcElt, {RngIntElt} -> BoolElt
\end{verbatim} }
\color{black}

Decides if $S$ is a local ideal of $T$ at $I$.

\color{blue}
\index{Ideal}
{\small \begin{verbatim}
Ideal(T, S) : TenSpcElt, List -> TenSpcElt
\end{verbatim} }
\color{black}

Returns the ideal of $T$ containing $S$.

\color{blue}
\index{Ideal}
{\small \begin{verbatim}
Ideal(T, D, C) : TenSpcElt, List, Any -> TenSpcElt
\end{verbatim} }
\color{black}

Returns the ideal of $T$ containing $D$ in the domain and $C$ in the codomain.

\color{blue}
\index{Ideal}
{\small \begin{verbatim}
Ideal(T, S) : TenSpcElt, TenSpcElt -> TenSpcElt
\end{verbatim} }
\color{black}

Returns the ideal of $T$ containing $S$ as a submap.

\color{blue}
\index{IsIdeal}
{\small \begin{verbatim}
IsIdeal(T, S) : TenSpcElt, TenSpcElt -> BoolElt
\end{verbatim} }
\color{black}

Decides if $S$ is an ideal of $T$.

\color{blue}
\index{LocalQuotient}
{\small \begin{verbatim}
LocalQuotient(T, S, I : \parameters) : TenSpcElt, TenSpcElt, {RngIntElt} -> TenSpcElt, Hmtp
Check : BoolElt : true
\end{verbatim} }
\color{black}

Returns the local quotient of $T$ by $S$ at $I$. If you know $S$ is a local 
ideal of $T$ at $I$, set {\tt Check} to {\tt false} to skip the verification.
A homotopism is also returned, mapping from $T$ to $T/S$.

\color{blue}
\index{Quotient!tensor}\index{/!tensor}
{\small \begin{verbatim}
Quotient(T, S : \parameters) : TenSpcElt, TenSpcElt -> TenSpcElt, Hmtp
Check : BoolElt : true
T / S : TenSpcElt, TenSpcElt -> TenSpcElt, Hmtp
\end{verbatim} }
\color{black}

Returns the quotient of $T$ by $S$. If you know $S$ is an ideal of $T$, 
set {\tt Check} to {\tt false} to skip the verification.
A homotopism is also returned, mapping from $T$ to $T/S$.

\begin{framed}{\bf Example} {\tt Ten\_Cat\_Ops}\\
{\small We will construct a quotient of tensors. First, we construct a subtensor from a random tensor.
\begin{lstlisting}[frame=single,basicstyle=\ttfamily\color{black!30!
teal},backgroundcolor=\color{white!70!gray}]
> T := RandomTensor(GF(5),[4,4,2]);
> T := RandomTensor(GF(5),[4,4,2]);
> T;
Tensor of valence 2, U2 x U1 >-> U0
U2 : Full Vector space of degree 4 over GF(5)
U1 : Full Vector space of degree 4 over GF(5)
U0 : Full Vector space of degree 2 over GF(5)
> 
> F := Frame(T);
> L := [* F[1]![1,1,1,0], F[2]![0,0,0,1], F[3]![0,0] *];
> S := Subtensor(T,L);
> S;
Tensor of valence 2, U2 x U1 >-> U0
U2 : Vector space of degree 4, dimension 1 over GF(5)
Generators:
(1 1 1 0)
Echelonized basis:
(1 1 1 0)
U1 : Vector space of degree 4, dimension 1 over GF(5)
Generators:
(0 0 0 1)
Echelonized basis:
(0 0 0 1)
U0 : Vector space of degree 2, dimension 1 over GF(5)
Generators:
(0 2)
Echelonized basis:
(0 1)
> 
> IsSubtensor(T,S);
true
\end{lstlisting}
Now we construct the ideal of $T$ containing $S$.
\begin{lstlisting}[frame=single,basicstyle=\ttfamily\color{black!30!
teal},backgroundcolor=\color{white!70!gray}]
> I := Ideal(T,S);
> I;
Tensor of valence 2, U2 x U1 >-> U0
U2 : Vector space of degree 4, dimension 1 over GF(5)
Generators:
(1 1 1 0)
Echelonized basis:
(1 1 1 0)
U1 : Vector space of degree 4, dimension 1 over GF(5)
Generators:
(0 0 0 1)
Echelonized basis:
(0 0 0 1)
U0 : Full Vector space of degree 2 over GF(5)
Generators:
(1 0)
(0 1)
> 
> IsIdeal(T,I);
true
\end{lstlisting}
Finally, we construct the quotient of $T$ by $I$.
\begin{lstlisting}[frame=single,basicstyle=\ttfamily\color{black!30!
teal},backgroundcolor=\color{white!70!gray}]
> T/I;
Tensor of valence 2, U2 x U1 >-> U0
U2 : Full Vector space of degree 3 over GF(5)
U1 : Full Vector space of degree 3 over GF(5)
U0 : Full Vector space of degree 0 over GF(5)
Maps from U2 x U1 >-> U0 to V2 x V1 >-> V0.
U2 -> V2: Mapping from: Full Vector space of degree 4 over 
GF(5) to Full Vector space of degree 3 over GF(5)
U1 -> V1: Mapping from: Full Vector space of degree 4 over 
GF(5) to Full Vector space of degree 3 over GF(5)
U0 -> V0: Mapping from: Full Vector space of degree 2 over 
GF(5) to Full Vector space of degree 0 over GF(5)
\end{lstlisting}
}
\end{framed}

\subsection{Categorical operations on tensor spaces}

We have categorical notions for tensor spaces as well.

\color{blue}
\index{SubtensorSpace}
{\small \begin{verbatim}
SubtensorSpace(T, L) : TenSpc, [TenSpcElt] -> TenSpc
\end{verbatim} }
\color{black}

Returns the subtensor space of $T$ generated by the tensors in the sequence $L$.

\color{blue}
\index{SubtensorSpace}
{\small \begin{verbatim}
SubtensorSpace(T, t) : TenSpc, TenSpcelt -> TenSpc
\end{verbatim} }
\color{black}

Returns the subtensor space of $T$ generated by the tensor $t$.

\color{blue}
\index{IsSubtensorSpace}
{\small \begin{verbatim}
IsSubtensorSpace(T, S) : TenSpc, TenSpc -> BoolElt
\end{verbatim} }
\color{black}

Decides if the tensor space $S$ is a subtensor space of $T$.

\color{blue}
\index{Quotient!tensor space}\index{/!tensor space}
{\small \begin{verbatim}
Quotient(T, S) : TenSpc, TenSpc -> TenSpc
T / S : TenSpc, TenSpc -> TenSpc
\end{verbatim} }
\color{black}

Returns the quotient tensor space of $T$ by $S$.

\begin{framed}{\bf Example} {\tt TenSpc\_Cat\_Ops}\\
{\small We construct a subtensor space.
\begin{lstlisting}[frame=single,basicstyle=\ttfamily\color{black!30!
teal},backgroundcolor=\color{white!70!gray}]
> T := KTensorSpace(GF(2),[4,4,2]);
> T;
Tensor space of dimension 32 over GF(2) with valence 2
U2 : Full Vector space of degree 4 over GF(2)
U1 : Full Vector space of degree 4 over GF(2)
U0 : Full Vector space of degree 2 over GF(2)
> 
> L := [ T.i : i in [1..Ngens(T)] | IsEven(i) ];
> S := SubtensorSpace(T, L);
> S;
Tensor space of dimension 16 over GF(2) with valence 2
U2 : Full Vector space of degree 4 over GF(2)
U1 : Full Vector space of degree 4 over GF(2)
U0 : Full Vector space of degree 2 over GF(2)
> SystemOfForms(Random(S));
[
    [0 0 0 0]
    [0 0 0 0]
    [0 0 0 0]
    [0 0 0 0],

    [1 0 0 0]
    [0 1 0 1]
    [1 0 0 0]
    [0 0 1 0]
]
\end{lstlisting}
Now we compute the quotient tensor space $Q=T/S$.
\begin{lstlisting}[frame=single,basicstyle=\ttfamily\color{black!30!
teal},backgroundcolor=\color{white!70!gray}]
> Q := T/S;
> Q;
Tensor space of dimension 16 over GF(2) with valence 2
U2 : Full Vector space of degree 4 over GF(2)
U1 : Full Vector space of degree 4 over GF(2)
U0 : Full Vector space of degree 2 over GF(2)
> SystemOfForms(Random(Q));
[
    [0 0 0 0]
    [1 1 1 0]
    [1 0 1 1]
    [1 1 0 1],

    [0 0 0 0]
    [0 0 0 0]
    [0 0 0 0]
    [0 0 0 0]
]
> SystemOfForms(Q![1 : i in [1..32]]);
[
    [1 1 1 1]
    [1 1 1 1]
    [1 1 1 1]
    [1 1 1 1],

    [0 0 0 0]
    [0 0 0 0]
    [0 0 0 0]
    [0 0 0 0]
]
\end{lstlisting}
}
\end{framed}

\section{Homotopisms}

Magma provides functions for homotopisms. Homotopisms are also equipped with a tensor category.

\subsection{Constructions of Homotopisms}~

\color{blue}
\index{Homotopism}
{\small \begin{verbatim}
Homotopism(T, S, M) : TenSpcElt, TenSpcElt, List -> Hmtp
Homotopism(T, S, M, C) : TenSpcElt, TenSpcElt, List, TenCat -> Hmtp
\end{verbatim} }
\color{black}

Returns the homotopism from $T$ to $S$ given by the list of maps $M$ and the category $C$. 
The default tensor category is the same as tensor categories for $T$ and $S$.

\begin{framed}{\bf Example} {\tt Hmtp\_Const}\\
{\small We illustrate how to construct tensor categories.
\begin{lstlisting}[frame=single,basicstyle=\ttfamily\color{black!30!
teal},backgroundcolor=\color{white!70!gray}]
> TS := KTensorSpace(GF(4),[2,3,4]);
> T := Random(TS);
> S := Random(TS);
> M := [* Random(KMatrixSpace(GF(4),i,i)) : i in [2..4] *];
> H := Homotopism(T,S,M);
> H;
Maps from U2 x U1 >-> U0 to V2 x V1 >-> V0.
U2 -> V2: 
[    1     0]
[    0     0]
U1 -> V1: 
[    0   $.1 $.1^2]
[    1     1   $.1]
[  $.1   $.1 $.1^2]
U0 -> V0: 
[    1     1   $.1 $.1^2]
[    0     1 $.1^2   $.1]
[    0     0   $.1 $.1^2]
[  $.1 $.1^2   $.1     0]
> 
> 
> M[2] := map< Frame(TS)[2] -> Frame(TS)[2] | x :-> x >;
> H2 := Homotopism(T,S,M);
> H2;
Maps from U2 x U1 >-> U0 to V2 x V1 >-> V0.
U2 -> V2: 
[    1     0]
[    0     0]
U1 -> V1: Mapping from: Full Vector space of degree 3 
over GF(2^2) to Full Vector space of degree 3 over GF(2^2) 
given by a rule [no inverse]
U0 -> V0: 
[    1     1   $.1 $.1^2]
[    0     1 $.1^2   $.1]
[    0     0   $.1 $.1^2]
[  $.1 $.1^2   $.1     0]
\end{lstlisting} }
\end{framed}


\subsection{Basic Operations with Homotopisms}

We provide some operations for homotopisms.

\color{blue}
\index{$*$!homotopism}
{\small \begin{verbatim}
H1 * H2 : Hmtp, Hmtp -> Hmtp
\end{verbatim} }
\color{black}

Returns the composition of the homotopisms $H_1$ and $H_2$.

\color{blue}
\index{Domain!homotopism}
{\small \begin{verbatim}
Domain(H) : Hmtp -> TenSpcElt
\end{verbatim} }
\color{black}

Returns the domain tensor of $H$.

\color{blue}
\index{Codomain!homotopism}
{\small \begin{verbatim}
Codomain(H) : Hmtp -> TenSpcElt
\end{verbatim} }
\color{black}

Returns the codomain tensor of $H$.

\color{blue}
\index{Maps}
{\small \begin{verbatim}
Maps(H) : Hmtp -> List
\end{verbatim} }
\color{black}

Returns the of maps for the various modules in the domain and codomain 
tensors.

\color{blue}
\index{TensorCategory!homotopism}
{\small \begin{verbatim}
TensorCategory(H) : Hmtp -> TenCat
\end{verbatim} }
\color{black}

Returns the tensor category of $H$.

\color{blue}
\index{ChangeTensorCategory!homotopism}
{\small \begin{verbatim}
ChangeTensorCategory(H, C) : Hmtp, TenCat -> Hmtp
ChangeTensorCategory(~H, C) : Hmtp, TenCat -> Hmtp
\end{verbatim} }
\color{black}

Changes the tensor category of $H$ to the given category.

\color{blue}
\index{Kernel}
{\small \begin{verbatim}
Kernel(H) : Hmtp -> TenSpcElt
\end{verbatim} }
\color{black}

Returns the kernel of $H$ as an ideal of its domain tensor.

\color{blue}
\index{Image!homotopism}
{\small \begin{verbatim}
Image(H) : Hmtp -> TenSpcElt
\end{verbatim} }
\color{black}

Returns the image of $H$ as a submap of the codomain tensor.

\begin{framed}{\bf Example} {\tt Hmtp\_Ops}\\
{\small We perform basic operations with homotopisms.
\begin{lstlisting}[frame=single,basicstyle=\ttfamily\color{black!30!
teal},backgroundcolor=\color{white!70!gray}]
> T := RandomTensor(GF(7),[5,4,3]);
> F := Frame(T);
> 
> I := [* hom< F[j] -> F[j] | [< F[j].i,F[j].i > :\
>   i in [1..Dimension(F[j])]] > : j in [1..3] *];
> H := Homotopism(T,T,I);
> 
> Image(H);
Tensor of valence 2, U2 x U1 >-> U0
U2 : Full Vector space of degree 5 over GF(7)
U1 : Full Vector space of degree 4 over GF(7)
U0 : Full Vector space of degree 3 over GF(7)
> Kernel(H);
Tensor of valence 2, U2 x U1 >-> U0
U2 : Vector space of degree 5, dimension 0 over GF(7)
U1 : Vector space of degree 4, dimension 0 over GF(7)
U0 : Vector space of degree 3, dimension 0 over GF(7)
\end{lstlisting}
If the tensor is over vector spaces, then matrices can be used to create a homotopism.
\begin{lstlisting}[frame=single,basicstyle=\ttfamily\color{black!30!
teal},backgroundcolor=\color{white!70!gray}]
> M := [* RandomMatrix(GF(7),i,i) : i in [5,4,3] *];
> G := Homotopism(T,T,M);
> G;
Maps from U2 x U1 >-> U0 to V2 x V1 >-> V0.
U2 -> V2: 
[5 3 6 0 4]
[6 0 0 1 1]
[6 4 3 1 5]
[3 4 6 1 4]
[2 4 1 3 2]
U1 -> V1: 
[5 5 0 3]
[3 5 1 3]
[3 6 1 5]
[2 3 5 4]
U0 -> V0: 
[6 4 1]
[2 6 5]
[1 2 3]
\end{lstlisting}
Homotopisms can be composed so long as Magma can compose each of the individual maps.
\begin{lstlisting}[frame=single,basicstyle=\ttfamily\color{black!30!
teal},backgroundcolor=\color{white!70!gray}]
> G*G;
Maps from U2 x U1 >-> U0 to V2 x V1 >-> V0.
U2 -> V2: 
[3 6 3 0 5]
[0 5 1 4 2]
[1 5 0 2 1]
[2 4 4 2 2]
[4 2 0 0 5]
U1 -> V1: 
[4 3 6 0]
[4 6 0 6]
[4 3 4 3]
[0 4 0 0]
U0 -> V0: 
[3 1 1]
[1 5 5]
[6 1 6]
\end{lstlisting}
We can change the underlying category for the homotopism $G$ to get a different morphism.
\begin{lstlisting}[frame=single,basicstyle=\ttfamily\color{black!30!
teal},backgroundcolor=\color{white!70!gray}]
> Cat := TensorCategory([1,-1,1],{{0},{1},{2}});
> G := Homotopism(T,T,M,Cat);
> G;
Maps from U2 x U1 >-> U0 to V2 x V1 >-> V0.
U2 -> V2: 
[5 3 6 0 4]
[6 0 0 1 1]
[6 4 3 1 5]
[3 4 6 1 4]
[2 4 1 3 2]
U1 <- V1: 
[5 5 0 3]
[3 5 1 3]
[3 6 1 5]
[2 3 5 4]
U0 -> V0: 
[6 4 1]
[2 6 5]
[1 2 3]
> 
> Image(G);
Tensor of valence 2, U2 x U1 >-> U0
U2 : Full Vector space of degree 5 over GF(7)
U1 : Vector space of degree 4, dimension 0 over GF(7)
U0 : Vector space of degree 3, dimension 2 over GF(7)
Echelonized basis:
(1 0 4)
(0 1 3)
> 
> Kernel(G);
Tensor of valence 2, U2 x U1 >-> U0
U2 : Vector space of degree 5, dimension 0 over GF(7)
U1 : Full Vector space of degree 4 over GF(7)
U0 : Vector space of degree 3, dimension 1 over GF(7)
Echelonized basis:
(1 4 0)
\end{lstlisting}
}
\end{framed}
