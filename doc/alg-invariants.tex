
\section{Invariants of nonassociative algebras}

Converting an algebra to a tensor enables Magma to compute standard invariants of any algebra. 
We note that there are known errors for $\mathbb{R}$ and $\mathbb{C}$ due to the numerical stability of the linear algebra involved in the computations.

\index{Center}\index{Centre}
\begin{intrinsics}
Center(A) : Alg -> Alg
Centre(A) : Alg -> Alg
\end{intrinsics}

Returns the center of the algebra $A$.

\index{Centroid!algebra}
\begin{intrinsics}
Centroid(A) : Alg -> AlgMat
\end{intrinsics}

Returns the centroid of the $K$-algebra $A$ as a subalgebra of $\text{End}_K(A)$.

\begin{example}[CenterCentroids]

We will construct a representation of $\mathfrak{sl}_2(9)$ in $\mathbb{M}_4(\mathbb{F}_3)$.
First we construct $\mathfrak{gl}_2(9)$ from $\mathbb{M}_4(\mathbb{F}_3)$.
\begin{code}
> M := MatrixAlgebra(GF(3), 4);
> f := ConwayPolynomial(3, 2);
> C := CompanionMatrix(f);
> I := IdentityMatrix(GF(3), 2);
> A := sub< M | [InsertBlock(M!0, X, i, j) : \
>     X in [I, C], i in [1, 3], j in [1, 3]] >;
> T := CommutatorTensor(A);
> T;
Tensor of valence 3, U2 x U1 >-> U0
U2 : Full Vector space of degree 8 over GF(3)
U1 : Full Vector space of degree 8 over GF(3)
U0 : Full Vector space of degree 8 over GF(3)
> gl2 := HeisenbergAlgebra(T);
> gl2;
Algebra of dimension 8 with base ring GF(3)
\end{code}

Our Lie algebra is not simple as it has a nontrivial center, so we will obtain $\mathfrak{sl}_2$ by factoring out the center.
Note that our algebras are over the prime field $\mathbb{F}_3$, so the center is 2-dimensional (over $\mathbb{F}_3$). 
Notice that $\mathfrak{sl}_2(9)$ has a trivial center but has a 2-dimensional centroid.
\begin{code}
> sl2 := gl2/Center(gl2);
> sl2;
Algebra of dimension 6 with base ring GF(3)
> Center(sl2);
Algebra of dimension 0 with base ring GF(3)
> Centroid(sl2);
Matrix Algebra of degree 6 with 2 generators over GF(3)
\end{code}
\end{example}

\index{LeftNucleus!algebra}\index{RightNucleus!algebra}\index{MidNucleus!algebra}
\begin{intrinsics}
LeftNucleus(A) : Alg -> AlgMat
RightNucleus(A) : Alg -> AlgMat
MidNucleus(A) : Alg -> AlgMat
\end{intrinsics}

Returns the nucleus of the algebra $A$ as a subalgebra of the enveloping algebra of right multiplication $\mathcal{R}(A)$.

\index{DerivationAlgebra!algebra}
\begin{intrinsics}
DerivationAlgebra(A) : Alg -> AlgMatLie
\end{intrinsics}

Returns the derivation algebra of the algebra $A$ as a Lie subaglebra of $\text{End}_K(A)$.

\begin{example}[DerivationAlg]

We will compute the derivation algebra of the (rational) octonions $\mathbb{O}$ and also the 27 dimension exceptional Jordan algebra $\mathfrak{H}_3(\mathbb{O})$.
Because the intrinsics use exact linear algebra, we do not use the more familiar field $\mathbb{C}$ in this context.
First we consider $\mathbb{O}$.
We verify that $\Der(\mathbb{O})\cong G_2$.
\begin{code}
> A := OctonionAlgebra(Rationals(), -1, -1, -1);
> A;
Algebra of dimension 8 with base ring Rational Field
> D := DerivationAlgebra(A);
> D;
Matrix Lie Algebra of degree 8 over Rational Field
> SemisimpleType(D);
G2
\end{code}

Now we will just briefly perform a sanity check and verify that \texttt{D} acts as it should.
\begin{code}
> a := Random(Basis(A));
> b := Random(Basis(A));
> del := Random(Basis(D));
> (a*b)*del eq (a*del)*b + a*(b*del);
true
\end{code}

Finally, we construct $\mathfrak{H}_3(\mathbb{O})$ the $3\times 3$ Hermitian matrices, and we verify that $\Der(\mathfrak{H}_3(\mathbb{O})\cong F_4$. 
\begin{code}
> J := ExceptionalJordanCSA(A);
> J;
Algebra of dimension 27 with base ring Rational Field
> D_J := DerivationAlgebra(J);
> Dimension(D_J);
52
> SemisimpleType(D_J);
F4
\end{code}
\end{example}
